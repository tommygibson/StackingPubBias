### Mavridis Copas model

data
{
  for(i in 1:S){
    s2[i] = s[i] ^ 2
  }
  s.min = min(s)
  s.max = max(s)
}

model
{
  for(i in 1:S){
    y[i] ~ dnorm(mean.y[i], prec.y[i])
    z[i] ~ dnorm(u[i], 1) T(0,)
    
    mean.y[i] = theta[i] + rho * s[i] * (z[i] - u[i])
    prec.y[i] = 1 / var.y[i]
    var.y[i] = s2[i] * (1 - (rho ^ 2))
    
    u[i] = gamma0 + gamma1 / s[i]
    
    theta[i] ~ dnorm(theta0, prec.theta)
  }
  
  gamma0 = c1 - (c2 - c1) / (1 / s.min + 1 / s.max) / s.max
  gamma1 = (c2 - c1) / (1 / s.min + 1 / s.max)
  
  c1 <- probit(p.low) 
  c2 <- probit(p.high) 
  
  p.low ~ dunif(L1, L2)
  p.high ~ dunif(U1, U2)
  
  theta0 ~ dnorm(0, 0.01)
  
  prec.theta = 1 / (tau ^ 2)
  tau ~ dnorm(0, .01) T(0,)
  rho ~ dunif(-1, 1)
  
}