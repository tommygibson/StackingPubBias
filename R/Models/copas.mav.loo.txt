### Mavridis Copas model with likelihood calculation
### This specific script is for re-running the model
### to calculate leave-one-out log-predicitive density
### for problematic observations

data
{
  for(i in 1:S){
    s2[i] = s[i] ^ 2
  }
  s.min = min(s)
  s.max = max(s)
}

model
{
  for(i in 1:S){
    y[i] ~ dnorm(mean.y[i], prec.y[i])
    z[i] ~ dnorm(u[i], 1) T(0,)
    
    mean.y[i] = theta0 + rho * s[i] * (z[i] - u[i])
    prec.y[i] = 1 / var.y[i]
    var.y[i] = tau2 + s2[i] * (1 - (rho ^ 2))
    
    u[i] = gamma0 + gamma1 / s[i]
    
    # calculating log-likelihood
    # unnecessary for non-loo observations
    # rho_bar[i] = rho * s[i] / sqrt(tau2 + s2[i])
    # v[i] = (u[i] + rho_bar[i] * (y[i] - theta0) / sqrt(tau2 + s2[i])) / sqrt(1 - rho_bar[i] ^ 2)
    # loglik[i] = log(pnorm(v[i], 0, 1)) + log(dnorm(y[i], theta0, 1 / (tau2 + s2[i]))) - log(pnorm(u[i], 0, 1))
    
  }
  
  gamma0 = c1 - (c2 - c1) / (1 / s.min + 1 / s.max) / s.max
  gamma1 = (c2 - c1) / (1 / s.min + 1 / s.max)
  
  c1 <- probit(p.low) 
  c2 <- probit(p.high) 
  
  p.low ~ dunif(L1, L2)
  p.high ~ dunif(U1, U2)
  
  theta0 ~ dnorm(0, .01)
  
  ## Tau prior is specified in Mavridis 2013
  tau ~ dnorm(0, .01) T(0,)
  tau2 = tau ^ 2
  rho ~ dunif(-1, 1)
  
  ### calculate log-likelihood for held-out observations
  for(i in 1:H){
    s2_h[i] = s_h[i]^2
    u_h[i] = gamma0 + gamma1 / s_h[i]
    rho_bar_h[i] = rho * s_h[i] / sqrt(tau2 + s2_h[i])
    v_h[i] = (u[i] + rho_bar_h[i] * (y_h[i] - theta0) / sqrt(tau2 + s2_h[i])) / sqrt(1 - rho_bar_h[i] ^ 2)
    loglik_h[i] = exp(log(pnorm(v_h[i], 0, 1)) + log(dnorm(y_h[i], theta0, 1 / (tau2 + s2_h[i]))) - log(pnorm(u_h[i], 0, 1)))
  }
  
}